;  button.s -- Set state of button.
;  Copyright (C) 2020 Dieter Baron
;
;  This file is part of Joyride, a controller test program for C64.
;  The authors can be contacted at <joyride@tpau.group>.
;
;  Redistribution and use in source and binary forms, with or without
;  modification, are permitted provided that the following conditions
;  are met:
;  1. Redistributions of source code must retain the above copyright
;     notice, this list of conditions and the following disclaimer.
;  2. The names of the authors may not be used to endorse or promote
;     products derived from this software without specific prior
;     written permission.
;
;  THIS SOFTWARE IS PROVIDED BY THE AUTHORS ``AS IS'' AND ANY EXPRESS
;  OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
;  WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
;  ARE DISCLAIMED.  IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR ANY
;  DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
;  DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE
;  GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
;  INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER
;  IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
;  OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN
;  IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

.export button, small_button, tiny_button

.autoimport +

.include "joyride.inc"

.code

; set state of button at ptr2 to A

button:
	tax
	ldy #0
loop:
	lda (ptr2),y
	and #$7f
	cpx #0
	beq clear
	ora #$80
clear:
	sta (ptr2),y
	iny
	cpy #3
	bne l2
	ldy #40
	bne loop
l2:
	cpy #43
	bne l3
	ldy #80
	bne loop
l3:
	cpy #83
	bne loop

	clc
	lda ptr2
	adc #3
	sta ptr2
	lda ptr2 + 1
	adc #0
	sta ptr2 + 1

	rts

small_button:
	tax
	ldy #1
small_loop:
	lda (ptr2),y
	and #$7f
	cpx #0
	beq :+
	ora #$80
:	sta (ptr2),y
	iny
	cpy #2
	bne :+
	ldy #40
	bne small_loop
:	cpy #43
	bne :+
	ldy #81
	bne small_loop
:	cpy #82
	bne small_loop
	rts

tiny_button:
	tax
	ldy #0
tiny_loop:
	lda (ptr2),y
	and #$7f
	cpx #0
	beq :+
	ora #$80
:	sta (ptr2),y
	iny
	cpy #2
	bne tiny_loop
	rts
